plugins {
    id "com.palantir.configuration-resolver" version "0.1.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "io.quartic.remote"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode (version.isInteger() ? version.toInteger() : 1)
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    signingConfigs {
        release {
            storeFile file("${System.properties['user.home']}/release-key.jks")
            storePassword System.env.ZIGGY_STORE_PASSWORD
            keyAlias "release-key"
            keyPassword System.env.ZIGGY_KEY_PASSWORD
        }
    }

    buildTypes {
        // Use Android Shrinker
        // See https://developer.android.com/studio/build/shrink-code.html
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        internal {
            applicationId "io.quartic.remote.internal"
        }
        
        real {
            applicationId "io.quartic.remote"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    // Required to avoid "Duplicate files copied in APK META-INF/license" error
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    dexOptions {
        preDexLibraries file("local.properties").exists()   // Prevents pointlessly running on CircleCI
    }
}

play {
    jsonFile = file(System.properties["user.home"] + "/" + "google-play-service-key.json")
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(":backend-api")
    compile project(":common-core")

    compile "com.android.support:appcompat-v7:25.1.0"
    compile "com.android.support:design:25.1.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.6"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.7.6"
    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"
    compile "com.squareup.retrofit2:converter-jackson:2.1.0"

    compile 'com.google.android.gms:play-services-location:10.0.1'

    compile "io.reactivex:rxkotlin:0.60.0"
    compile "io.reactivex:rxjava-async-util:0.21.0"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "com.jakewharton.rxbinding:rxbinding-kotlin:1.0.0"

    testCompile "junit:junit:4.12"
    testCompile "com.github.tomakehurst:wiremock:2.2.2"
    testCompile "com.fasterxml.jackson.core:jackson-databind:2.7.6" // Explicit spec of transitive dep to avoid version conflicts
    testCompile "com.fasterxml.jackson.core:jackson-annotations:2.7.6" // Explicit spec of transitive dep to avoid version conflicts

    // Required for instrumented tests
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}
