plugins {
    id "com.palantir.configuration-resolver" version "0.1.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "io.quartic.app"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode (version.isInteger() ? version.toInteger() : 0)
        versionName version
    }

    signingConfigs {
        release {
            // TODO: figure out where to put these
            storeFile file("release-key.jks")
            storePassword "quartic"
            keyAlias "release-key"
            keyPassword "quartic"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    // Required to avoid "Duplicate files copied in APK META-INF/license" error
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    dexOptions {
        preDexLibraries file("local.properties").exists()   // Prevents pointlessly running on CircleCI
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(":backend-api")

    compile "com.android.support:appcompat-v7:25.1.0"
    compile "com.android.support:design:25.1.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.6"
    compile "com.squareup.retrofit2:retrofit:2.1.0"
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"
    compile "com.squareup.retrofit2:converter-jackson:2.1.0"

    compile 'com.google.android.gms:play-services:10.0.1'

    compile "io.reactivex:rxkotlin:0.60.0"
    compile "io.reactivex:rxjava-async-util:0.21.0"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "com.jakewharton.rxbinding:rxbinding-kotlin:1.0.0"

    testCompile "junit:junit:4.12"
    testCompile "com.github.tomakehurst:wiremock:2.2.2"
    testCompile "com.fasterxml.jackson.core:jackson-databind:2.7.6" // Explicit spec of transitive dep to avoid version conflicts
    testCompile "com.fasterxml.jackson.core:jackson-annotations:2.7.6" // Explicit spec of transitive dep to avoid version conflicts
}
